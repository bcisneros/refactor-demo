plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0' // PIT plugin
}

group = 'demo'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
    }

    afterSuite { desc, result ->
        if (!desc.parent) { // solo al final de la ra√≠z
            println "\n==== Test Summary ===="
            println "Result: ${result.resultType}"
            println "Tests: ${result.testCount}, " +
                    "Passed: ${result.successfulTestCount}, " +
                    "Failed: ${result.failedTestCount}, " +
                    "Skipped: ${result.skippedTestCount}"
            println "======================\n"
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

pitest {
    pitestVersion = '1.15.0' // PIT core version
    junit5PluginVersion = '1.2.1' // Required for JUnit 5
    targetClasses = ['demo.legacy.*'] // Classes to mutate
    targetTests = ['demo.legacy.*']   // Tests to run
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
}
